import org.apache.tools.ant.filters.ReplaceTokens

apply from: "docker.gradle"

allprojects {
    group = 'com.thoughtmechanix'
    version = '0.0.1-SNAPSHOT'
}

buildscript {
    ext {
        springBootVersion = '2.0.0.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('com.bmuschko:gradle-docker-plugin:3.2.5')
    }
}

subprojects {
    ext {
        scalaVersion = "2.12.4"
        springCloudVersion = "Finchley.M8"
        chapter = "chapter4"
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://repo.spring.io/libs-milestone' }
    }

    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.bmuschko.docker-java-application'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {
        compile group: 'org.scala-lang', name: "scala-compiler", version: scalaVersion

        testCompile('org.scalatest:scalatest_2.12:3.0.4')
        testCompile('org.mockito:mockito-core:2.13.0')
        testCompile('org.pegdown:pegdown:1.6.0')
    }

    task replaceTokens(type: Copy) {
        def finalBuildName = project.name + '-' + project.version

        from 'src/main/docker'
        into 'build/docker'
        filter(ReplaceTokens, tokens: ['project.build.finalName': finalBuildName])
    }


    task scalaTest(dependsOn: ['testClasses'], type: JavaExec) {
        main = 'org.scalatest.tools.Runner'
        args = ['-R', "build/classes/test",
                '-u', "build/test-results/$name",
                '-h', "build/resports/scalaTests/$name",
                '-o'
        ]
        classpath = sourceSets.test.runtimeClasspath
    }

    test.dependsOn scalaTest

    task copyJar(type: Copy) {
        dependsOn assemble
        from file("${project.buildDir}/libs")
        into file("${project.buildDir}/docker")
    }

    docker {
        url = dockerHost()
        certPath = dockerCertPath()
    }
}



